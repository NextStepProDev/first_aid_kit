plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${dependencyManagementVersion}"
    id 'jacoco'
}

group = 'com.firstaid'
version = '0.2.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

// NOTE: do NOT put gradle properties (versions) in this file.
// Put them in gradle.properties (project root).

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    runtimeOnly 'org.postgresql:postgresql'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation "com.github.ben-manes.caffeine:caffeine:${caffeineVersion}"

    // Rate limiting
    implementation "com.github.vladimir-bukhtoyarov:bucket4j-core:${bucket4jVersion}"

    // Resilience4j for circuit breaker and retry
    implementation "io.github.resilience4j:resilience4j-spring-boot3:${resilience4jVersion}"
    implementation "io.github.resilience4j:resilience4j-retry:${resilience4jVersion}"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${openapiVersion}"

    implementation "com.github.librepdf:openpdf:${librePdfVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // JWT support
    implementation "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    implementation "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-restclient-test'
    testImplementation 'org.springframework.boot:spring-boot-resttestclient'

    testImplementation 'org.springframework.boot:spring-boot-webmvc-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'

    // Import Testcontainers BOM (platform) BEFORE declaring testcontainers artifacts
    testImplementation platform("org.testcontainers:testcontainers-bom:${testcontainersVersion}")

    testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers-postgresql'
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    useJUnitPlatform()

    // nie rozwiązujemy classpath w konfiguracji — dopiero przy uruchomieniu testu
    doFirst {
        def agentFile = configurations.testRuntimeClasspath.files.find { it.name.contains('byte-buddy-agent') }
        if (agentFile) {
            // dodajemy javaagent dopiero na etapie wykonania zadania
            jvmArgs = (jvmArgs ?: []) + ["-javaagent:${agentFile.absolutePath}"]
        }
    }

    systemProperty "spring.profiles.active", "test"
    testLogging {
        events "passed", "skipped", "failed"
    }
}

compileJava.options.encoding = 'UTF-8'

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = '0.80'.toBigDecimal()
                }
            }
        }
    }
}
